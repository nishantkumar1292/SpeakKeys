plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.plugin.serialization'
    id 'com.mikepenz.aboutlibraries.plugin'
    id 'org.jetbrains.kotlin.plugin.compose'
}

def isBundleBuild = gradle.startParameter.taskNames.any { it.toLowerCase().contains("bundle") }


android {
    compileSdk = 35
    defaultConfig {
        applicationId "com.elishaazaria.sayboard"
        minSdk = 23
        targetSdk = 35
        versionCode = 402011
        versionName = "v4.2.2"
    }
    buildTypes {
        release {
            minifyEnabled true
            shrinkResources = true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    splits {
        abi {
            enable = !isBundleBuild
            reset()
            include "arm64-v8a", "armeabi-v7a", "x86", "x86_64"
            universalApk = true
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }
    buildFeatures {
        viewBinding = true
        compose = true
        buildConfig = true
    }

    namespace = 'com.elishaazaria.sayboard'

    packagingOptions {
        resources {
            excludes += '/META-INF/{AL2.0,LGPL2.1}'
        }
    }

    lint {
        lintConfig = file("app/lint.xml")
    }
}

def abiCodes = ["armeabi-v7a": 1, "arm64-v8a": 2, "x86": 3, "x86_64": 4]
androidComponents {
    onVariants(selector().all()) { variant ->
        variant.outputs.each { output ->
            def abiFilter = output.filters.find { it.filterType.name() == "ABI" }?.identifier
            if (!isBundleBuild) {
                if (output.hasProperty("outputFileName")) {
                    output.outputFileName.set("SpeakKeys_${abiFilter ?: "Universal"}.apk")
                }
                def abiVersionCode = abiCodes[abiFilter]
                if (abiVersionCode != null) output.versionCode.set(402011 + abiVersionCode)
            }
        }
    }
}

dependencies {
    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    implementation 'com.google.android.material:material:1.11.0'

    implementation 'androidx.lifecycle:lifecycle-livedata-ktx:2.7.0'

    // OkHttp for cloud API calls
    implementation 'com.squareup.okhttp3:okhttp:4.12.0'

    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-core:1.7.0'
    implementation "org.jetbrains.kotlinx:kotlinx-serialization-json:1.6.0"

    implementation 'dev.patrickgold.jetpref:jetpref-datastore-model:0.1.0-beta14'
    implementation 'dev.patrickgold.jetpref:jetpref-datastore-ui:0.1.0-beta14'
    implementation 'dev.patrickgold.jetpref:jetpref-material-ui:0.1.0-beta14'

    implementation "org.burnoutcrew.composereorderable:reorderable:0.9.6"

    def composeBom = platform('androidx.compose:compose-bom:2023.10.00')
    implementation composeBom
    androidTestImplementation composeBom

    implementation 'androidx.compose.material:material'

    // Android Studio Preview support
    implementation 'androidx.compose.ui:ui-tooling-preview'
    debugImplementation 'androidx.compose.ui:ui-tooling'

    // UI Tests
    androidTestImplementation 'androidx.compose.ui:ui-test-junit4'
    debugImplementation 'androidx.compose.ui:ui-test-manifest'

    implementation 'androidx.compose.material:material-icons-extended'
    implementation 'androidx.compose.material3:material3-window-size-class'

    implementation 'androidx.activity:activity-compose:1.8.2'
    implementation 'androidx.compose.runtime:runtime-livedata'

    implementation "com.mikepenz:aboutlibraries-compose:11.1.4"
}
