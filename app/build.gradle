apply plugin: 'com.android.application'
apply plugin: 'org.jetbrains.kotlin.android'
apply plugin: 'org.jetbrains.kotlin.plugin.serialization'
apply plugin: 'com.mikepenz.aboutlibraries.plugin'
apply plugin: 'org.jetbrains.kotlin.plugin.compose'

def isBundleBuild = gradle.startParameter.taskNames.any { it.toLowerCase().contains("bundle") }


android {
    compileSdk 35
    defaultConfig {
        applicationId "com.elishaazaria.sayboard"
        minSdkVersion 23
        targetSdkVersion 35
        versionCode 402010
        versionName "v4.2.1"
    }
    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    splits {
        abi {
            enable !isBundleBuild
            reset()
            include "arm64-v8a", "armeabi-v7a", "x86", "x86_64"
            universalApk true
        }
    }

    if (!isBundleBuild) {
        android.applicationVariants.configureEach { variant ->
            variant.outputs.configureEach { output ->
                var abi_name = output.getFilter(output.ABI) ? output.getFilter(output.ABI) : "Universal"
                output.outputFileName = "SpeakKeys_${abi_name}.apk"
            }
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
    buildFeatures {
        viewBinding true
        compose true
        buildConfig true
    }

    namespace 'com.elishaazaria.sayboard'

    packagingOptions {
        resources {
            excludes += '/META-INF/{AL2.0,LGPL2.1}'
        }
    }

    lint {
        lintConfig = file("app/lint.xml")
    }
}

if (!isBundleBuild) {
    //ext.abiCodes = ['armeabi-v7a':1, x86:2, x86_64:3]
    ext.abiCodes = ["armeabi-v7a": 1, "arm64-v8a": 2, "x86": 3, "x86_64": 4]

    // For each APK output variant, override versionCode with a combination of
    // ext.abiCodes * 1000 + variant.versionCode. In this example, variant.versionCode
    // is equal to defaultConfig.versionCode. If you configure product flavors that
    // define their own versionCode, variant.versionCode uses that value instead.
    android.applicationVariants.configureEach { variant ->

        // Assigns a different version code for each output APK
        // other than the universal APK.
        variant.outputs.each { output ->

            // Stores the value of ext.abiCodes that is associated with the ABI for this variant.
            def variantVersionCode =
                    // Determines the ABI for this variant and returns the mapped value.
                    project.ext.abiCodes.get(output.getFilter(output.ABI))

            // Because abiCodes.get() returns null for ABIs that are not mapped by ext.abiCodes,
            // the following code doesn't override the version code for universal APKs.
            // However, because you want universal APKs to have the lowest version code,
            // this outcome is desirable.
            if (variantVersionCode != null) {

                // Assigns the new version code to versionCodeOverride, which changes the
                // version code for only the output APK, not for the variant itself. Skipping
                // this step causes Gradle to use the value of variant.versionCode for the APK.
                output.versionCodeOverride =
                        variant.versionCode + variantVersionCode
            }
        }
    }
}

dependencies {
    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    implementation 'com.google.android.material:material:1.11.0'

    implementation 'androidx.lifecycle:lifecycle-livedata-ktx:2.7.0'

    // OkHttp for cloud API calls
    implementation 'com.squareup.okhttp3:okhttp:4.12.0'

    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-core:1.7.0'
    implementation "org.jetbrains.kotlinx:kotlinx-serialization-json:1.6.0"

    implementation 'dev.patrickgold.jetpref:jetpref-datastore-model:0.1.0-beta14'
    implementation 'dev.patrickgold.jetpref:jetpref-datastore-ui:0.1.0-beta14'
    implementation 'dev.patrickgold.jetpref:jetpref-material-ui:0.1.0-beta14'

    implementation "org.burnoutcrew.composereorderable:reorderable:0.9.6"

    def composeBom = platform('androidx.compose:compose-bom:2023.10.00')
    implementation composeBom
    androidTestImplementation composeBom

    implementation 'androidx.compose.material:material'

    // Android Studio Preview support
    implementation 'androidx.compose.ui:ui-tooling-preview'
    debugImplementation 'androidx.compose.ui:ui-tooling'

    // UI Tests
    androidTestImplementation 'androidx.compose.ui:ui-test-junit4'
    debugImplementation 'androidx.compose.ui:ui-test-manifest'

    implementation 'androidx.compose.material:material-icons-extended'
    implementation 'androidx.compose.material3:material3-window-size-class'

    implementation 'androidx.activity:activity-compose:1.8.2'
    implementation 'androidx.compose.runtime:runtime-livedata'

    implementation "com.mikepenz:aboutlibraries-compose:11.1.4"
}
